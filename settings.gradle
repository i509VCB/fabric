pluginManagement {
	repositories {
		jcenter()
		gradlePluginPortal()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		mavenLocal()
	}
}

rootProject.name = "fabric-api"

include 'fabric-api-base'

include 'fabric-command-api-v1'
include 'fabric-content-registries-v0'
include 'fabric-crash-report-info-v1'

include 'fabric-events-interaction-v0'
include 'fabric-game-rule-api-v1'
include 'fabric-item-api-v1'
include 'fabric-item-groups-v0'
include 'fabric-lifecycle-events-v1'
include 'fabric-loot-tables-v1'
include 'fabric-networking-v0'
include 'fabric-networking-blockentity-v0'
include 'fabric-object-builder-api-v1'
include 'fabric-particles-v1'
include 'fabric-registry-sync-v0'
include 'fabric-resource-loader-v0'
include 'fabric-screen-handler-api-v1'
include 'fabric-structure-api-v1'
include 'fabric-tag-extensions-v0'
include 'fabric-tool-attribute-api-v1'

// Client only modules
["fabric-blockrenderlayer-v1",
 "fabric-key-binding-api-v1",
 "fabric-models-v0",
 "fabric-renderer-api-v1",
 "fabric-renderer-indigo",
 "fabric-renderer-registries-v1",
 "fabric-rendering-v1",
 "fabric-rendering-data-attachment-v1",
 "fabric-rendering-fluids-v1",
 "fabric-textures-v0"].each {
	include it
	project(":$it").projectDir = file("client/$it")
}

// Modules which need updating to latest version (not included)
[/*"fabric-dimensions-v1"*/].forEach { String it ->
	include it
	project(":$it").projectDir = file("updating/$it")
}

// Experimental modules
["fabric-biome-api-v1"].forEach {
	include it
	project(":$it").projectDir = file("experimental/$it")
}

// Deprecated modules
["fabric-commands-v0",
 "fabric-containers-v0",
 "fabric-events-lifecycle-v0",
 "fabric-keybindings-v0",
 "fabric-mining-levels-v0",
 "fabric-object-builders-v0",
 "fabric-rendering-v0"].each {
	include it
	project(":$it").projectDir = file("deprecated/$it")
}
